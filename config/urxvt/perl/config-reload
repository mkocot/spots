#! perl

use v5.20;
# NOTE(m): removed
# use common::sense;

use File::Basename qw(dirname);

my $libdir = dirname(__FILE__);

our @terms = ();
my $cmd = "";

sub reload_all {
    my @lines = `urxvt --perl-lib $libdir -pe config-print 2>&1`;
    my %resource;
    for (@lines) {
        $resource{$1} = $2 if (/(.*?): (.*)/);
    }
    for (keys %resource) {
        my $key;
        given ($_) {
            # Codes adapted from https://github.com/sos4nt/dynamic-colors/blob/master/bin/dynamic-colors
            $key = "4;$1" when (/color(\d+)/);
            $key = 10 when "foreground";
            $key = 11 when "background";
            $key = 12 when "cursorColor";
            $key = 13 when "pointerColor2";
            $key = 14 when "pointerColor";
            $key = 17 when "highlight";
            $key = 708 when "borderColor";
            $key = 710 when "font";
            when ("geometry") {
                if (my ($col, $row) = $resource{$_} =~ /(\d+)x(\d+)/) {
                    $cmd .= "\e[8;${row};${col}t";
                }
                next;
            }
            default { next }
        }
        my $val = $resource{$_};
        $cmd .= "\e]$key;$val\a";
    }
    # warn "CMD: " . ($cmd =~ s/\e/\\e/gr =~ s/\a/\\a/gr);
    for (@terms) {
        $_->cmd_parse($cmd);
    }
}

our $watch = urxvt::anyevent->signal (
		signal => "HUP", # which event to wait for (SIGHUP)
                cb   => sub {    # what callback to execute
                    reload_all;
                }
            );

sub on_start {
    my ($ext) = @_;
    # warn "START ext=$ext TERM=$TERM";
    $TERM->cmd_parse($cmd);
    push @terms, $TERM;
    ();
}

sub on_destroy {
    my ($ext) = @_;
    # warn "END ext=$ext TERM=$TERM";
    @terms = grep { $_ != $TERM } @terms;
    if (!@terms) { undef $watch; }
    ();
}
